#!/usr/bin/perl -w

use encoding 'utf8';
use strict;
use warnings;

use XML::LibXML;

# Read qstat information into XML DOM.
open (QSTAT, '-|', '/usr/bin/qstat', '-u', '*', '-xml') or die ("Couldn't read qstat");
my $DOM = XML::LibXML->load_xml (IO => *QSTAT) or die ($!);
close (QSTAT);

# Initialize counters.
my %Counters = ('sge_pending' => 0,
                'sge_running' => 0,
                'sge_error'   => 0);

# Iterate over all jobs.
foreach my $Node ($DOM->findnodes (q!//job_list/state!)) {
    my $State = $Node->textContent ();
    if ($State =~ /^qw/) {
        $Counters {'sge_pending'}++;
    } elsif ($State =~ /[rRt]/) {
        $Counters {'sge_running'}++;
    } elsif ($State =~ /E/) {
        $Counters {'sge_error'}++;
    }
}

# Update Ganglia.
for my $Name (keys (%Counters)) {
    system ('/usr/bin/gmetric', '--group', 'SGE', '--name', $Name, '--value', $Counters {$Name}, '--type', 'uint16') and die ($!);
}
